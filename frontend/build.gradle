apply plugin: 'kotlin2js'

compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/js/module/todomvc.js"
    kotlinOptions.suppressWarnings = true
    kotlinOptions.verbose = true
    kotlinOptions.moduleKind = "amd" // we use amd because this is a frontend project
    kotlinOptions.sourceMap = true
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
    compile("org.jetbrains.kotlinx:kotlinx-html-js:$kotlinxHtmlVersion")
    compile("org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion")
}

compileKotlin2Js.doFirst {
    // This is necessary because kotlin2js does not clean up between builds
    delete "${projectDir}/js/module/"
}

// we copy over the necessary js files (like kotlin.js) from the jar files we included
build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/js/module/"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
}